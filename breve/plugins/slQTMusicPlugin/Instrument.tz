@include "Abstract.tz"

@plugin "QTInstrumentPlugin.o" (slInitQTInstrumentFuncs).

Abstract : Instrument {
    + variables:
        instrumentInfo (pointer).
        lastNote (int).
        autoStop (int).

    + to init:
        lastNote = -1.
        autoStop = 1.

    + to init-as-piano:
        % If not already initialized, sets this instrument to be a piano.

        instrumentInfo = newInstrument(1).
        return self.

    + to init-as-violin:
        % If not already initialized, sets this instrument to be a violin.

        instrumentInfo = newInstrument(41).
        return self.

    + to init-as-cello:
        % If not already initialized, sets this instrument to be a cello.

        instrumentInfo = newInstrument(43).
        return self.

    + to init-as-vibraphone:
        % If not already initialized, sets this instrument to be a vibraphone.

        instrumentInfo = newInstrument(12).
        return self.

    + to init-as-instrument number n (int):
        instrumentInfo = newInstrument(n).
        return self.

    + to play-note number n (int) for-duration duration (double):
        playNote(instrumentInfo, n, 80, duration).

    + to volume-play-note number n (int) for-duration duration (double) with-volume vol (int):
        playNote(instrumentInfo, n, vol, duration).

    + to play-chord with-notes notes (list) for-duration duration (double):
        playChord(instrumentInfo, notes, 80, duration).

    + to volume-play-chord with-notes notes (list) for-duration duration (double) with-volume vol (int):
        playChord(instrumentInfo, notes, vol, duration).

    + to start-note number n (int):
        if lastNote != -1 && autoStop: stopNote(instrumentInfo, lastNote).
        startNote(instrumentInfo, n, 80).
        lastNote = n.

    + to volume-start-note number n (int) with-volume vol (int):
        if lastNote != -1 && autoStop: stopNote(instrumentInfo, lastNote).
        startNote(instrumentInfo, n, vol).
        lastNote = n.

    + to stop-last-note:
        if lastNote != -1: stopNote(instrumentInfo, lastNote).
        lastNote = -1.

    + to destroy:
        if instrumentInfo: freeInstrument(instrumentInfo).
}
