@include "Abstract.tz"

@plugin "QTInstrumentPlugin.o" (slInitQTInstrumentFuncs).

Abstract : Instrument {
    + variables:
        instrumentInfo (pointer).
        lastNote (int).
        autoStop (int).

    + to init:
        lastNote = -1.
        autoStop = 1.

    + to init-as-piano:
        % If not already initialized, sets this instrument to be a piano.

        instrumentInfo = instrumentNew(1).
        return self.

    + to init-as-violin:
        % If not already initialized, sets this instrument to be a violin.

        instrumentInfo = instrumentNew(41).
        return self.

    + to init-as-cello:
        % If not already initialized, sets this instrument to be a cello.

        instrumentInfo = instrumentNew(43).
        return self.

    + to init-as-vibraphone:
        % If not already initialized, sets this instrument to be a vibraphone.

        instrumentInfo = instrumentNew(12).
        return self.

    + to init-as-instrument number n (int):
        instrumentInfo = instrumentNew(n).
        return self.

    + to play-note number n (int) for-duration duration (double) with-velocity v = 80 (int):
        instrumentPlayNote(instrumentInfo, n, v, duration).

    + to play-chord with-notes notes (list) for-duration duration (double) with-velocity v = 80 (int):
        instrumentPlayChord(instrumentInfo, notes, v, duration).

    + to start-note number n (int) with-velocity v = 80 (int):
        if lastNote != -1 && autoStop: instrumentStopNote(instrumentInfo, lastNote).
        instrumentStartNote(instrumentInfo, n, v).
        lastNote = n.

    + to stop-last-note:
        if lastNote != -1: instrumentStopNote(instrumentInfo, lastNote).
        lastNote = -1.

	+ to set-pitch-bend to value (double):
		instrumentSetController(instrumentInfo, 32, value * 128).

	+ to set-modulation to value (double):
		instrumentSetController(instrumentInfo, 1, value * 512).

	+ to set-balance to value (double):
		instrumentSetController(instrumentInfo, 10, 256 + (value * 256)).

	+ to set-midi-controller number n (int) to value (int):
		instrumentSetController(instrumentInfo, n, value).

    + to destroy:
        if instrumentInfo: instrumentFree(instrumentInfo).
}
