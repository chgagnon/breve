@include "Abstract.tz"

Abstract : Data (aka Datum) [version 1.8] {
	% <b>This class is now considered obsolete and should no longer be used</b>.
	% See the section 
	% <a href="http://www.spiderland.org/breve/docs/docs/archiving.html">Archiving and Dearchiving</a> of the breve documentation for more details.
	% <p>
	% The now obsolete class description follows.
	% <p>
    % The Data class is a special class that can be saved to disk, loaded in 
    % to simulations and otherwise manipulated in ways that other classes 
    % cannot.  Because of the features of this class, it may only contain 
    % variables of type int, float or vector.
    % <p>
	% Previous versions of breve used a binary format for saving and loading
	% data objects--the current version saves as XML files.  A method for 
	% loading binary objects from previous versions still exists METHOD(load-from-file), 
	% but its use is depreciated.

    + to init:
        if dataCheckVariables() != 0:
            die "subclasses of class Data may not variables of type object or pointer.".

    + to save-with-dialog:
        % Presents the user with a save dialog box in order to get a 
        % filename, and then saves the contents of this instance to 
        % the specified filename.
		% <p>
		% <b>Previous versions of breve used a binary save of the object.  The current
		% version saves using an XML file format.</b>

        return dataWriteObjectWithDialog().

    + to load-with-dialog:
        % Presents the user with a save dialog box in order to get a 
        % filename, and then loads the contents of the specified filename
        % into this instance.  The chosen file must contain a previously
        % saved instance of the same object.
		% <p>
		% <b>Previous versions of breve used a binary save of the object.  The current
		% version loads files saved in the XML file format.</b>

        return dataReadObjectWithDialog().

    + to copy-contents to otherInstance (object):
        % Copies the contents of this data object to otherInstance.  The
        % instances must be of the same type.

        dataCopyObject(otherInstance).

    + to save-to file theFile (string):
        % This method is obsolete.  See METHOD(save-as-xml) instead.

        return dataWriteObject(theFile).

    + to load-from file theFile (string):
		% This method is obsolete.  You should use METHOD(save-as-xml) and METHOD(load-from-xml)
		% for saving and loading objects from now on.  This method is still supported for loading
		% in instances saved in older versions of breve.
		% <p>
        % Loads the current instance from theFile.  The file is saved in the current "default" 
        % directory which <b>may or may not be explicitly defined</b>, depending on which 
        % <b>breve</b> implementation is being used.  For implementations in which the 
        % user is not able to define a default directory, the default should be a "reasonable"
        % location such as UNIX home directory or Mac OS X "Documents" directory.
        % <p>
        % Unless you are absolutely sure of which implementation and configuration will 
        % be used when your simulation is run, you should use save-with-dialog instead.

        return dataReadObject(theFile).

    + to send to theHost (string) on-port thePort (int):
		% Used in conjunction with OBJECT(NetworkServer) to send the contents of this object
		% over the network.

        return sendXMLObject(theHost, thePort, self).

	+ to load-from-xml file theFile (string):
		% Loads the contents of saved XML file theFile into this object.  The XML file
		% must contain a previously archived instance of the same class (archived using
		% METHOD(save-as-xml)).

		dataReadXMLObject(theFile).

	+ to save-as-xml file theFile (string):
		% Saves the contents of this instance to an XML file called theFile.  It can
		% later be loaded back into another instance of the same class using 
		% METHOD(load-from-xml).

		dataWriteXMLObject(theFile).
}
